CMAKE_MINIMUM_REQUIRED(VERSION 2.6)
PROJECT(DEVSBENCH CXX C)
ENABLE_TESTING()

SET(DEVSBENCH_MAJOR 1)
SET(DEVSBENCH_MINOR 0)
SET(DEVSBENCH_PATCH 0)
SET(DEVSBENCH_EXTRA "")
SET(DEVSBENCH_VERSION "${DEVSBENCH_MAJOR}.${DEVSBENCH_MINOR}.${DEVSBENCH_PATCH}")
SET(DEVSBENCH_VERSION_SHORT "${DEVSBENCH_MAJOR}.${DEVSBENCH_MINOR}")
SET(DEVSBENCH_NAME "devsbench")
SET(DEVSBENCH_NAME_VERSION "${DEVSBENCH_NAME}-${DEVSBENCH_VERSION}")
SET(DEVSBENCH_LOCALE_NAME "${DEVSBENCH_NAME}-${DEVSBENCH_MAJOR}-${DEVSBENCH_MINOR}")
IF ("${DEVSBENCH_EXTRA}" STREQUAL "")
  SET(DEVSBENCH_COMPLETE "${DEVSBENCH_MAJOR}.${DEVSBENCH_MINOR}.${DEVSBENCH_PATCH}")
ELSE ("${DEVSBENCH_EXTRA}" STREQUAL "")
  SET(DEVSBENCH_COMPLETE "${DEVSBENCH_MAJOR}.${DEVSBENCH_MINOR}.${DEVSBENCH_PATCH}-${DEVSBENCH_EXTRA}")
ENDIF ("${DEVSBENCH_EXTRA}" STREQUAL "")
SET(DEVSBENCH_NAME_COMPLETE "${DEVSBENCH_NAME}-${DEVSBENCH_COMPLETE}")

#
 # Installation variables
#

SET(DEVSBENCH_LIBRARY_DIRS "lib/devsbench-${DEVSBENCH_VERSION_SHORT}")
SET(DEVSBENCH_INCLUDE_DIRS "include/devsbench-${DEVSBENCH_VERSION_SHORT}/devsbench")
SET(DEVSBENCH_INCLUDE_DIR "include/devsbench-${DEVSBENCH_VERSION_SHORT}")
SET(DEVSBENCH_MANPAGE_PATH "share/man/man1")
SET(DEVSBENCH_SHARE_DIRS "share/devsbench-${DEVSBENCH_VERSION_SHORT}")

#
 # Append the library version information to the library target
 # properties.
#

SET (DEVSBENCH_LIBRARY_PROPERTIES VERSION 0)
SET (DEVSBENCH_APP_PROPERTIES SOVERSION "${DEVSBENCH_MAJOR}.${DEVSBENCH_MINOR}"
  VERSION "${DEVSBENCH_MAJOR}.${DEVSBENCH_MINOR}")

#
 # CPack configuration
#

INCLUDE(CMakeCPack.cmake)

#
 # Debug mode
#

IF (CMAKE_COMPILER_IS_GNUCC AND CMAKE_COMPILER_IS_GNUCXX)
  SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -Wextra")
  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -std=c++11 -pthread")
  IF (UNIX)
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -pedantic")
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -pedantic")
  ENDIF (UNIX)
ENDIF (CMAKE_COMPILER_IS_GNUCC AND CMAKE_COMPILER_IS_GNUCXX)

if (CMAKE_BUILD_TYPE STREQUAL "")
  SET(CMAKE_BUILD_TYPE "RelWithDebInfo" CACHE STRING "Choose the type of build" FORCE)
endif ()

#
 # Modules
#

INCLUDE(CheckIncludeFileCXX)
INCLUDE(CheckIncludeFile)
INCLUDE(CheckLibraryExists)
INCLUDE(CMakeDetermineCCompiler)

#
 # Check libraries with pkgconfig
#

FIND_PACKAGE(PkgConfig REQUIRED)

# FIXME use old-style link directories for now
IF (COMMAND CMAKE_POLICY)
  CMAKE_POLICY(SET CMP0003 OLD)
ENDIF (COMMAND CMAKE_POLICY)

PKG_CHECK_MODULES(GLIBMM REQUIRED glibmm-2.4)
#
 # Test the libboost header and libboost-text library.
#

OPTION(WITH_TEST "use the libboost test unit library [default: on]" ON)
SET(Boost_USE_MULTITHREADED ON)
SET(Boost_USE_STATIC_LIBS OFF)
SET(Boost_NO_SYSTEM_PATHS OFF)
SET(Boost_USE_STATIC_RUNTIME ON)
IF (WIN32)
  SET(Boost_THREADAPI "win32")
ENDIF (WIN32)
SET(Boost_DETAILED_FAILURE_MSG FALSE)
SET(Boost_DEBUG FALSE)

FIND_PACKAGE(Boost COMPONENTS graph timer system)
IF (NOT Boost_GRAPH_FOUND)
  MESSAGE(FATAL_ERROR "The boost graph library is required")
ENDIF (NOT Boost_GRAPH_FOUND)
IF (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "35")
  SET(Boost_SYSTEM_LIBRARY "")
ENDIF (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "35")
IF (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "41")
  SET(DEVSBENCH_HAVE_BOOST_SPIRIT2 0 CACHE INTERNAL "" FORCE)
ELSE (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "41")
  SET(DEVSBENCH_HAVE_BOOST_SPIRIT2 1 CACHE INTERNAL "" FORCE)
ENDIF (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "41")

#
 # Generate the config.h
#

CHECK_INCLUDE_FILE_CXX(cxxabi.h DEVSBENCH_HAVE_GCC_ABI_DEMANGLE)
CHECK_INCLUDE_FILE_CXX(execinfo.h DEVSBENCH_HAVE_EXECINFO)
CHECK_INCLUDE_FILE_CXX(signal.h DEVSBENCH_HAVE_SIGNAL)
INCLUDE_DIRECTORIES(${DEVSBENCH_BINARY_DIR})

#
 # Generate the vle.pc pkg-config file.
 # Build some variable to build a correct Win32 pkg config file with:
 # prefix=/target to detect the DIRNAME
 # cflag=-Iboost_1_34_1 for the directory
#

SET(DEVSBENCH_PKGCONFIG_PREFIXPATH "${CMAKE_INSTALL_PREFIX}")
SET(DEVSBENCH_PKGCONFIG_BOOSTINCLUDE_DIRS "${Boost_INCLUDE_DIRS}")
SET(DEVSBENCH_PKGCONFIG_LIBDIR "lib")

CONFIGURE_FILE(${PROJECT_SOURCE_DIR}/src/devsbench.pc.in
  ${DEVSBENCH_BINARY_DIR}/devsbench-${DEVSBENCH_VERSION_SHORT}.pc @ONLY)
INSTALL(FILES ${DEVSBENCH_BINARY_DIR}/devsbench-${DEVSBENCH_VERSION_SHORT}.pc
  DESTINATION lib/pkgconfig)

#
 # Install the README
#

IF(UNIX)
  INSTALL(FILES "README" DESTINATION "${DEVSBENCH_SHARE_DIRS}")
  INSTALL(FILES "COPYING" DESTINATION "${DEVSBENCH_SHARE_DIRS}")
ELSE(UNIX)
  INSTALL(FILES "README" DESTINATION "${DEVSBENCH_SHARE_DIRS}" RENAME "Readme.txt")
  INSTALL(FILES "COPYING" DESTINATION "${DEVSBENCH_SHARE_DIRS}" RENAME "CopyRight.txt")
ENDIF(UNIX)

#
 # Browse the src subdirectory
#

ADD_SUBDIRECTORY(src)

#
 # Status
#

MESSAGE(STATUS "- - - -")
MESSAGE(STATUS "${DEVSBENCH_NAME_COMPLETE} configured successfully")
MESSAGE(STATUS "Using ${CMAKE_INSTALL_PREFIX} for installation")
MESSAGE(STATUS "Build type ${CMAKE_BUILD_TYPE}")
IF (DEVSBENCH_HAVE_GCC_ABI_DEMANGLE)
  MESSAGE(STATUS "Build with GCC ABI Demangle...: yes")
ENDIF (DEVSBENCH_HAVE_GCC_ABI_DEMANGLE)
IF (DEVSBENCH_HAVE_EXECINFO)
  MESSAGE(STATUS "Build with execinfo.h.........: yes")
ENDIF (DEVSBENCH_HAVE_EXECINFO)
IF (Boost_FOUND)
  MESSAGE(STATUS "Build with boost..............: yes")
ENDIF (Boost_FOUND)
MESSAGE(STATUS "- - - -")

# vim:tw=0:ts=8:tw=0:sw=2:sts=2
